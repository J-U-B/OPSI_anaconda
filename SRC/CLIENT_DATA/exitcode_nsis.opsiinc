;=====================================================================
; exitcode_nsis.opsiinc
; Source: 
;	https://forum.opsi.org/wiki/userspace:check_nullsoft-exitcode
;	http://nsis.sourceforge.net/Docs/AppendixD.html#errorlevels
;
; Version: 1.1
; Date   : 2020-11-29 23:04:00 +0100
; Author :J. Boettge <boettge@mpi-halle.mpg.de>
;=====================================================================
encoding=utf8

;=====================================================================
[sub_check_exitcode_nsis]
;=====================================================================
; DefVar $currentLogLevel$

set $currentLogLevel$ = getLogLevel

;*** ExitCode des Installers abfragen
set $ExitCode$   = getLastExitCode
set $ErrorMsg$   = ""
set $FatalError$ = ""
 
;*** Loglevel anpassen
setLogLevel = 4
 
 
;*** ExitCode auswerten
Switch $ExitCode$
	Case "0"
		setLogLevel = 5
		comment "ExitCode = " + $ExitCode$ + " Normal execution (no error)"
		setLogLevel = $currentLogLevel$
	EndCase
	 
	Case "1"
		setLogLevel = 5
		set $ErrorMsg$ = "ExitCode = " + $ExitCode$ + " Installation aborted by user (cancel button)"
		set $FatalError$ = "true"
	EndCase
	 
	Case "2"
		setLogLevel = 5
		set $ErrorMsg$ = "ExitCode = " + $ExitCode$ + " Installation aborted by script"
		set $FatalError$ = "true"
	EndCase
	
	DefaultCase
		setLogLevel = 5
		set $ErrorMsg$ = "ExitCode = " + $ExitCode$ + " Unknown error"
		set $FatalError$ = "true"
	EndCase
EndSwitch

if ($FatalError$ = "true")
	logError $ErrorMsg$
endif
 
;***Loglevel anpassen
setLogLevel = $currentLogLevel$
