;=====================================================================
; product specific variables ANACONDA
;
; J. Boettge <boettge@mpi-halle.mpg.de>  2019-10-18 12:36:19 +0200
;=====================================================================

include_insert "%ScriptPath%\variables.opsiinc"

Set $MAP$ 				= getProductMap
Set $FullProductId$   	= getValue("id", $MAP$)
Set $ProductName$     	= getValue("name", $MAP$)
Set $Version$         	= getValue("ProductVersion", $MAP$)
Set $ActionRequest$		= getValue("actionreuest", $MAP$)
Set $State$				= getValue("installationstate", $MAP$)
Set $SystemType$      	= GetSystemType
Set $NTVersion$   		= GetMsVersionInfo

;-----------------------------------------------------------
;*** basic requirements
;-----------------------------------------------------------
requiredWinstVersion >= "4.11.6"

;-----------------------------------------------------------
;*** helper functions
;-----------------------------------------------------------
importlib "helpers.opsifunc"

if CompareDotSeparatedNumbers($NTVersion$, "<", "6.1")
	LogError "OS is not Windows 7 or above"
	isFatalError
endif

if not ($SystemType$ = "64 Bit System")
	; LogError "OS is not a 64 Bit System"
	; isFatalError

	LogWarning "OS is not a 64 Bit System; 32 Bit is not explicitly supported"
endif

;-----------------------------------------------------------
;*** get LogLevel from property
Set $LogLevelDefault$ = GetProductProperty("log_level","default")
;-----------------------------------------------------------
;*** detect testing package:
if (strPos($FullProductId$, "0_") = "0") and (strPos($FullProductId$, "test_") = "0")
	; not a testing package
	set $ProductId$  	= $FullProductId$ 
	set $Testing$ 		= "false"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "{{DEFAULT_RELEASE_LOGLEVEL}}"
	endif
else
	; this is a testing package
	comment "Prefix for testing package detected"
	set $ProductId$  		= strPart($FullProductId$, calculate(strPos($FullProductId$,"_") + "+1"), strLength($FullProductId$))
	set $Testing$ 		= "true"
	if ($LogLevelDefault$ = "default")
		set $LogLevelDefault$  	= "{{DEFAULT_TESTING_LOGLEVEL}}"
	endif
endif
setlogLevel 				= $LogLevelDefault$

;-----------------------------------------------------------
;*** detect DFN package:
;-----------------------------------------------------------
if (strPos($FullProductId$, "o4i_") = "0") AND (strPos($FullProductId$, "dfn_") = "0")
	set $isDFN$ = "false"
	set $StrippedProductId$ = $ProductId$
else
	set $isDFN$ = "true"
	set $StrippedProductId$ = strPart($ProductId$,"5",strLength($ProductId$))
endif

;-----------------------------------------------------------
;*** Images
;-----------------------------------------------------------
Set $Img_Base$ = "anaconda"
if ($isDFN$ = "true")
	set $Img_Install$		= "%ScriptPath%\images\" + $Img_Base$ + "_install_dfn.png"
else
	set $Img_Install$		= "%ScriptPath%\images\" + $Img_Base$ + "_install.png"
endif
set $Img_Install_Custom$	= "%ScriptPath%\images\" + $Img_Base$ + "_install_custom.png"
Set $Img_Uninstall$			= "%ScriptPath%\images\" + $Img_Base$ + "_uninstall.png"
Set $Img_Uninstall_Custom$	= "%ScriptPath%\images\" + $Img_Base$ + "_uninstall_custom.png"
Set $Img_Prepare$			= "%ScriptPath%\images\" + $Img_Base$ + "_prepare.png"
Set $Img_Plugins$			= "%ScriptPath%\images\" + $Img_Base$ + "_plugins.png"
Set $Img_Update$			= "%ScriptPath%\images\" + $Img_Base$ + "_update.png"
Set $Img_Upgrade$			= "%ScriptPath%\images\" + $Img_Base$ + "_upgrade.png"

comment "preparing..."
ShowBitmap $Img_Prepare$ $ProductName$

;-----------------------------------------------------------
;*** software architecture; valid values: "32 bit", "64 bit", "sysnative"
;-----------------------------------------------------------
; make the systemtype shorter:
if ($SystemType$ = "64 Bit System")
	set $SystemType$ ="x64"
else
	set $SystemType$ ="x32"
endif

{{#ifdef_64bit_only}}
Set $sw_arch$ = "64 bit"
{{/ifdef_64bit_only}}
{{^ifdef_64bit_only}}
Set $sw_arch$ = GetProductProperty("install_architecture","sysnative")
{{/ifdef_64bit_only}}

Set $sw_arch$ = takeString(0, splitString($sw_arch$, " "))

if ( $sw_arch$ = "sysnative" )
	if ($SystemType$ = "x64")
		Set $sw_arch$ = "64"
	else
		Set $sw_arch$ = "32"
	endif
endif

if ($sw_arch$ = "64")
	; 64 bit software
	set $InstallBase$ = "%ProgramFiles64Dir%"
	set $sArch$ = "_64"
else
	; 32 bit software
	set $InstallBase$ = EnvVar("ProgramFiles(x86)")
	set $sArch$ = ""
endif

if ($SystemType$="x86 System") and ($sw_arch$="64")
	LogError "Cannot install 64 bit version of " + $ProductName$ + " on a 32 bit operating system!"
	isFatalError
endif

;-----------------------------------------------------------
;*** property: language - "de_DE, "en_US"
; application language defined via environment variable LANG
;-----------------------------------------------------------
; Set $sw_lang$ 				= GetProductProperty("default_language","en_US")
set $sw_lang$ = "default"

;-----------------------------------------------------------
;*** property: create desktop/startmenu/quickstart link?
;-----------------------------------------------------------
Set $pr_DesktopLink_Navigator$ 		= GetProductProperty("link_desktop_Anaconda_Navigator","False")
Set $pr_DesktopLink_Spyder$ 		= GetProductProperty("link_desktop_Spyder","False")
Set $pr_DesktopLink_Jupyter$ 		= GetProductProperty("link_desktop_Jupyter_Notebook","False")
Set $pr_DesktopLink_AnacondaPrompt$	= GetProductProperty("link_desktop_Anaconda_Prompt","False")

Set $pr_SkipUpdate$ 				= GetProductProperty("update_skip","False")
Set $pr_SkipFixRights$ 				= GetProductProperty("update_rights_skip","False")
Set $pr_DryUpdate$ 					= GetProductProperty("update_dry_run","False")
Set $pr_VerboseUpdate$ 				= GetProductProperty("update_verbose","False")
Set $pr_ReleaseUpgrade$				= GetProductProperty("upgrade_release","False")
Set $pr_MinimumSpace$				= Trim(GetProductProperty("required_mimimum_space","default"))

Set $pr_AddPack_InstallMode$		= GetProductProperty("additional_packages_install_mode","none")
Set $TempList$				= emptyList($TempList$)
Set $pr_AddPackList$				= GetProductPropertyList("additional_packages",$TempList$)

;-----------------------------------------------------------
;*** properties: post install/uninstall
;-----------------------------------------------------------
set $CustomPostInstall$ 	= getProductProperty("custom_post_install","none")
set $CustomPostUninstall$ 	= getProductProperty("custom_post_uninstall","none")

;-----------------------------------------------------------
;*** misc settings
;-----------------------------------------------------------
{{^ifdef_PSDETAIL_DEBUG}}
set $PSDETAIL$ 			= "%ScriptPath%\bin\psdetail.exe"
if (CompareDotSeparatedNumbers($NTVersion$, ">=", "10.0"))
	set $PSDETAIL$ 			= "%ScriptPath%\bin\psdetail4.exe"
endif
{{/ifdef_PSDETAIL_DEBUG}}
{{#ifdef_PSDETAIL_DEBUG}}
set $PSDETAIL$ 			= "%ScriptPath%\bin\psdetail_debug.exe"
if (CompareDotSeparatedNumbers($NTVersion$, ">=", "10.0"))
	set $PSDETAIL$ 			= "%ScriptPath%\bin\psdetail4_debug.exe"
endif
{{/ifdef_PSDETAIL_DEBUG}}
set $PSDETAIL_mod$			= ""
set $Kill_Running$ 			= GetProductProperty("kill_running", "False")

Set $RegPathUninstall64$ 	= "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall"
Set $RegPathUninstall32$ 	= "HKEY_LOCAL_MACHINE\SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall"

;-------------------------------------------------------
; filter for opsiServiceCall productOnClient_getObjects:
;-------------------------------------------------------
set $ProductFilter$	= "*" + $StrippedProductId$
set $ProductVersionFilter$ = "*"

;-----------------------------------------------------------
;*** specific settings
;-----------------------------------------------------------
Set $InstallerType$   	= "NSIS"
include_append "%ScriptPath%\exitcode_nsis.opsiinc"


; packages (for example):
; 		Anaconda3-5.1.0-Windows-x86_64.exe
; 		Anaconda2-5.1.0-Windows-x86.exe
;Set $Installer$		  	= "%ScriptPath%\files\" + $StrippedProductId$ + "-" + $Version$ + "-Windows-x86" + $sArch$ + ".exe"
Set $Installer$		  	= "%ScriptPath%\files\" + $ProductName$ + "-" + $Version$ + "-Windows-x86" + $sArch$ + ".exe"

Set $InstallDir$      	= $InstallBase$ + "\" + stringReplace($ProductName$," ", "") + "\"
;Set $InstallDir$      	= $InstallBase$ + "\" + $ProductName$ + "\"
Set $InstDir$     	 	= $InstallBase$ + "\" + stringReplace($ProductName$," ", "")

;default value for required space (in MB):
Set $MinimumSpaceDefault$ = "9000"

;Set $MainBin$       	= $StrippedProductId$ + ".exe"
Set $MainBin$       	= "Scripts\anaconda-navigator.exe"
;Set $EXE$				= CreateStringList('python.exe', 'pythonw.exe', 'Scripts\Spyder.exe', 'Scripts\pandoc.exe', 'Scripts\anaconda.exe', 'Scripts\anaconda-navigator.exe', 'Scripts\jupyter-notebook.exe', 'Scripts\jupyter-qtkonsole.exe')
Set $EXE$				= CreateStringList('python.exe', 'pythonw.exe', 'Scripts\%.exe')

Set $AppName$			= $ProductName$ + " Navigator"
Set $AppIcon$			= $InstallDir$ + "\Menu\Anaconda-navigator.ico"
Set $AppTarget$			= '"' + $InstallDir$ + 'pythonw.exe"'
Set $AppParameters$		= '-q "' + $InstallDir$ + 'cwp.py" "' + $InstallDir$ + '." "' +  $InstallDir$ + 'pythonw.exe" "' + $InstallDir$ + 'Scripts\anaconda-navigator-script.py"'


Set $SearchPattern$ 	= $ProductName$
Set $Uninst_ID_std$	   	= $ProductName$
Set $Uninst_Prg$       	= "Uninstall-"+ $StrippedProductId$ + ".exe"
Set $Uninst_Prg_Hidden$ = "Un_A.exe"
Set $Uninstaller$	   	= $InstallDir$ + $Uninst_Prg$
; ...oder UninstallString aus Registry

; https://conda.io/docs/user-guide/install/windows.html
Set $Inst_Opts$        	= "/InstallationType=AllUsers  /S  /D=" + $InstallDir$
Set $Uninst_Opts$      	= "/S"


Set $ActivateBat$		= $InstallDir$ + "Scripts\activate.bat"
Set $CondaUpgradeCmd$	= "conda install -y -q anaconda python conda"
Set $CondaUpdateCmd$	= "conda update -y -q --all"
Set $CondaCleanCmd$		= "conda clean -y --all"
Set $CondaInstallCmd$	= "conda install -y"
