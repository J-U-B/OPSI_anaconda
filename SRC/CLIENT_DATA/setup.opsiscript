;======================================================================
; setup.opsiscript ANACONDA
; Installer-Typ: NSIS
; 
; J. Boettge <boettge@mpi-halle.mpg.de>  2018-06-28 13:43:50 +0200
;======================================================================

;======================================================================
[Initial]
;======================================================================
setLogLevel=5
; Log Errors in Logfile and abort:
ExitOnError=true
; Show syntax errors in the script:
ScriptErrorMessages=off
; Dont trace step by step through the script:
TraceMode=off 
; Let started programs run in front of the winst window
StayOnTop=false 

;======================================================================
[Actions]
;======================================================================
include_insert "%ScriptPath%\product_variables.opsiinc"

if FileExists("%ScriptPath%\add_packages.opsiinc")
	include_append "%ScriptPath%\add_packages.opsiinc"
	set $Has_AddPackages$ = "true"
else
	logError "File not found: add_packages.opsiinc"
	set $Has_AddPackages$ = "false"
endif


;=== Check free space

Set $MinimumSpace$  = $MinimumSpaceDefault$
if ( not ($pr_MinimumSpace$ = "default"))
	; strip "." and ",":
	set $pr_MinimumSpace$ = stringReplace($pr_MinimumSpace$, ".", "")
	set $pr_MinimumSpace$ = stringReplace($pr_MinimumSpace$, ",", "")
	if (isNumber($pr_MinimumSpace$))
		if ((strPos($pr_MinimumSpace$,"+") = "1") OR (strPos($pr_MinimumSpace$ ,"-") = "1"))
			set $MinimumSpace$  = calculate($MinimumSpace$+$pr_MinimumSpace$)
		else
			set $MinimumSpace$  = $pr_MinimumSpace$
		endif
		if ($MinimumSpace$ INT<= "0")
			logError "A negative value for required space or no space needed? Sure? - Using default value"
		endif
	else
		logError "Given value for [required_mimimum_space] is not an interger! Check the property setting. - Using default value."
		Set $MinimumSpace$  = $MinimumSpaceDefault$
	endif
endif
comment "Minimum space required: " + $MinimumSpace$ + " MB"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$ + " MB"))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " MB" + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	;=== remove previous installation
	if FileExists("%ScriptPath%\delsub.opsiinc")
		comment  "Starting uninstall sub section"
		; sub "%ScriptPath%\delsub.opsiinc"	
		include_append "%ScriptPath%\delsub.opsiinc"
		sub_delsub_main
	else
		logError "Can't find uninstaller script"
		isFatalError "delsub.opsiinc not found"
	endif	
	
	; Start the installation
	ShowBitmap $Img_Install$ $ProductName$
	Message "Installing " + $ProductName$ + " " + $Version$+ " (" + $sw_arch$ + " bit)"
	
	marktime	
	
	if not (FileExists($Installer$))
		logError "Installer not found: [" + $Installer$ + "]"
		isFatalError
	endif
	
	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode_nsis
	
	set $CheckBin$ = $InstallDir$+$MainBin$
	if not (FileExists($CheckBin$))
		logError "Main binary not found! [" + $CheckBin$ + "]"
		isFatalError
	endif
	
	if (($pr_AddPack_InstallMode$ = "setup") AND ($Has_AddPackages$ = "true"))
		set $TimeDiff$ = getDiffTimeSec
		comment  "Elapsed time after basic setup: " + $TimeDiff$ + " seconds"
		Sub_Add_Packages
		; restore install image
		ShowBitmap $Img_Install$ $ProductName$
	endif
	
	;=== create desktop link(s) if requested
	if ($pr_DesktopLink_AnacondaPrompt$ = "true")
		LinkFolder_CreateDesktopLink_AnacondaPrompt
	endif
	if ($pr_DesktopLink_Jupyter$ = "true")
		LinkFolder_CreateDesktopLink_Jupyter
	endif
	if ($pr_DesktopLink_Spyder$ = "true")
		LinkFolder_CreateDesktopLink_Spyder
	endif
	if ($pr_DesktopLink_Navigator$ = "true")
		LinkFolder_CreateDesktopLink_Navigator
	endif
	
	;=== custom post install
	comment "include custom post install file"
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			ShowBitmap $Img_Install_Custom$ $ProductName$
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
			ShowBitmap $Img_Install$ $ProductName$
		endif
	endif
	
	;=== measure installation time
	set $TimeDiff$ = getDiffTimeSec
	Message  "Setup time: " + $TimeDiff$ + " seconds"	
	
endif

 
;======================================================================
[Winbatch_install]
;======================================================================
"$Installer$" $Inst_Opts$ 
 
 
;=====================================================================
[LinkFolder_CreateDesktopLink_AnacondaPrompt]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $ProductName$ Prompt
       target: "%windir%\System32\cmd.exe"
       parameters: /K ""$InstDir$\Scripts\activate.bat" "$InstDir$""
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\Iconleak-Atrous-Console.ico
       icon_index:
end_link 
 
;=====================================================================
[LinkFolder_CreateDesktopLink_Jupyter]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: Jupyter Notebook
       target: $AppTarget$
       parameters: -q "$InstDir$\cwp.py" "$InstDir$" "$InstDir$\python.exe" "$InstDir$\Scripts\jupyter-notebook-script.py" %USERPROFILE%
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\jupyter.ico
       icon_index:
end_link 
 
;=====================================================================
[LinkFolder_CreateDesktopLink_Spyder]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: Spyder
       target: $AppTarget$
       parameters: -q "$InstDir$\cwp.py" "$InstDir$" "$InstDir$\pythonw.exe" "$InstDir$\Scripts\spyder-script.py"
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\spyder.ico
       icon_index:
end_link 
 
;=====================================================================
[LinkFolder_CreateDesktopLink_Navigator]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $AppName$
       target: $AppTarget$
       parameters: $AppParameters$
       working_dir: %CommonProfileDir%\Documents
       icon_file: $AppIcon$
       icon_index:
end_link



