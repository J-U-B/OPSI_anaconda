;===============================================================================
; checkinstance.opsiinc
; J. Boettge <boettge@mpi-halle.mpg.de> 2018-10-23 11:29:15 +0200
;===============================================================================

;===============================================================================
[sub_checkinstance]
;===============================================================================
comment "Checking for running instances and kill it (if allowed)."

set $PSDETAIL_mod$ 	= "-c"

if FileExists($PSDETAIL$)
	if (count($EXE$) = "0")
		; empty list of binaries, use only the one defined
		if not($MainBin$="")
			for %executable% in CreateStringList($MainBin$) do sub_CheckBin
		endif
	else
		; loop through list of possible binaries
		for %executable% in $EXE$ do sub_CheckBin
	endif
else
	logError "can't find psdetail.exe"
endif

;===============================================================================
[sub_CheckBin]
;===============================================================================
set $CheckBin$ = "%executable%"
if not ($CheckBin$ = "")
	winbatch_psdetail_count
	set $Proc_Cnt$ = getLastExitCode
	if $Proc_Cnt$ > "0"
		if $Kill_Running$ = "True"
			comment $Proc_Cnt$ + " running instance(s) of " + $CheckBin$ + " found, killing them."
			winbatch_psdetail_kill
			set $Proc_Killed$ = getLastExitCode
			comment $Proc_Killed$ + " instance(s) of " + $CheckBin$ + " were killed."
			if not ($Proc_Cnt$=$Proc_Killed$)
				logWarning "Number of killed instances (" + $Proc_Killed$ + ") does not match expectations (" + $Proc_Cnt$ + ")."
			endif
		else
			; logError $Proc_Cnt$+" running instance(s) of " + $CheckBin$ + " found, but I'm not allowed to kill them."
			; isFatalError "running instance"
			logWarning $Proc_Cnt$ + " running instance(s) of " + $CheckBin$ + " found, but I'm not allowed to kill them."
			isSuspended			
		endif
	else
		if $Proc_Cnt$ = "0"
			comment "no running instance of " + $CheckBin$ + " found"
		else
			logError "psdetail returns " + $Proc_Cnt$ + " (unexpected)"
		endif
	endif
endif

;===============================================================================
[winbatch_psdetail_count]
;===============================================================================
"$PSDETAIL$" $psdetail_mod$ "$CheckBin$"

;===============================================================================
[winbatch_psdetail_kill]
;===============================================================================
"$PSDETAIL$" -k $psdetail_mod$ "$CheckBin$"
