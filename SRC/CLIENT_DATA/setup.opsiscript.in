;======================================================================
; setup.opsiscript ANACONDA
; Installer-Typ: NSIS
;
; Jens Boettge <boettge@mpi-halle.mpg.de>  2023-03-30 10:46:59 +0200
;======================================================================

;======================================================================
[Initial]
;======================================================================
encoding=utf8
setLogLevel=5
; Log Errors in Logfile and abort:
ExitOnError=true
; Show syntax errors in the script:
ScriptErrorMessages=off
; Dont trace step by step through the script:
TraceMode=off 
; Let started programs run in front of the winst window
StayOnTop=false 

;======================================================================
[Actions]
;======================================================================
include_insert "%ScriptPath%\product_variables.opsiinc"

if FileExists("%ScriptPath%\add_packages.opsiinc")
	include_append "%ScriptPath%\add_packages.opsiinc"
	set $Has_AddPackages$ = "true"
else
	logError "File not found: add_packages.opsiinc"
	set $Has_AddPackages$ = "false"
endif


;=== Check free space

Set $MinimumSpace$  = $MinimumSpaceDefault$
if ( not ($pr_MinimumSpace$ = "default"))
	; strip "." and ",":
	set $pr_MinimumSpace$ = stringReplace($pr_MinimumSpace$, ".", "")
	set $pr_MinimumSpace$ = stringReplace($pr_MinimumSpace$, ",", "")
	if (isNumber($pr_MinimumSpace$))
		if ((strPos($pr_MinimumSpace$,"+") = "1") OR (strPos($pr_MinimumSpace$ ,"-") = "1"))
			set $MinimumSpace$  = calculate($MinimumSpace$+$pr_MinimumSpace$)
		else
			set $MinimumSpace$  = $pr_MinimumSpace$
		endif
		if ($MinimumSpace$ INT<= "0")
			logError "A negative value for required space or no space needed? Sure? - Using default value"
		endif
	else
		logError "Given value for [required_mimimum_space] is not an integer! Check the property setting. - Using default value."
		Set $MinimumSpace$  = $MinimumSpaceDefault$
	endif
endif
comment "Minimum space required: " + $MinimumSpace$ + " MB"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$ + " MB"))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " MB" + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	;=== remove previous installation
	if FileExists("%ScriptPath%\delsub.opsiinc")
		comment  "Starting uninstall sub section"
		; sub "%ScriptPath%\delsub.opsiinc"	
		include_append "%ScriptPath%\delsub.opsiinc"
		sub_delsub_main
	else
		logError "Can't find uninstaller script"
		isFatalError "delsub.opsiinc not found"
	endif	

{{^ifdef_legacy}}
	; Start the installation
	marktime

	if $pr_localCopy$ = "True"
		ShowBitmap $Img_Prepare$ $ProductName$
		Message "Copying the installer to local disk"
		Files_CopyLocalInstaller
		set $Installer$ = $TempDir$ + "\" + $InstallerFile$
	else
		set $Installer$ = $InstallerSrc$
	endif

	ShowBitmap $Img_Install$ $ProductName$
	Message "Installing " + $ProductName$ + " " + $Version$+ " (" + $sw_arch$ + " bit)"	
	
	if not (FileExists($Installer$))
		logError "Installer not found: [" + $Installer$ + "]"
		isFatalError
	endif
	
	;=== run the installer
	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode_nsis

	if $pr_localCopy$ = "True"
		comment "delete local installer copy: [" + $TempDir$ + "\" + $InstallerFile$ = "]"
		Files_DeleteLocalInstaller
	endif

	if not($FatalError$ = "")
		isFatalError "Installer returns fatal error!"
	endif
	
	if (($pr_AddPack_InstallMode$ = "setup") AND ($Has_AddPackages$ = "true"))
		set $TimeDiff$ = getDiffTimeSec
		comment  "Elapsed time after basic setup: " + $TimeDiff$ + " seconds"
		Sub_Add_Packages
		; restore install image
		ShowBitmap $Img_Install$ $ProductName$
	endif
	
	;=== create desktop link(s) if requested
	if ($pr_DesktopLink_AnacondaPrompt$ = "true")
		LinkFolder_CreateDesktopLink_AnacondaPrompt
	endif
	if ($pr_DesktopLink_AnacondaPwsh$ = "true")
		LinkFolder_CreateDesktopLink_AnacondaPowerShell
	endif
    if ($pr_DesktopLink_QtConsole$ = "true")
		LinkFolder_CreateDesktopLink_QtConsole
	endif
	if ($pr_DesktopLink_Jupyter$ = "true")
		LinkFolder_CreateDesktopLink_Jupyter
	endif
	if ($pr_DesktopLink_Spyder$ = "true")
		LinkFolder_CreateDesktopLink_Spyder
	endif
	if ($pr_DesktopLink_Navigator$ = "true")
		LinkFolder_CreateDesktopLink_Navigator
	endif
	
	;=== custom post install
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			ShowBitmap $Img_Install_Custom$ $ProductName$
			comment "Starting custom post install"
			include_append "%ScriptPath%\custom\" + $CustomPostInstall$
			SUB_POST_INSTALL
			ShowBitmap $Img_Install$ $ProductName$
		else
			logError "Can't find custom post install script: %ScriptPath%\custom\" + $CustomPostInstall$
			; isFatalError "delsub.opsiinc not found"
		endif
	endif	
	
	if ($pr_SkipUpdate$ = "true")
		Message "Skipping update after setup by property"
		noUpdateScript
	endif

    if ($pr_ClearPackageCache$ = "true")
        Message "Clearing package cache"
        ; reduce log level for a while
        if (getLogLevel INT> "5")
            setLogLevel = 5
        endif
        Files_DeletePackageCache
        setLogLevel = $LogLevelDefault$
    endif

{{/ifdef_legacy}}

	;=== measure installation time
	set $TimeDiff$ = getDiffTimeSec
	if $pr_localCopy$ = "True"
		Message  "Setup time:  " + $TimeDiff$ + " seconds  (includes time for local copy)"
	else
		Message  "Setup time: " + $TimeDiff$ + " seconds"
	endif
	
endif

{{#ifdef_legacy}}
;=== Prepare the change from dfn_ to o4i_
; Stage 1 - logWarning
; Stage 2 - logError (entire functionality)
; Stage 3 - logError (NO functionality except uninstall; no binaries)
; Stage 4 - removal of this package from repository pending
;   
;-----------------------------------------
; This is stage 3
;-----------------------------------------
if  ("{{legacy_level}}" = "1")
	logWarning "*******************************************************************************************"
	logWarning "This package is DEPRECATED!"
	logWarning "The functionality is completely taken over by o4i_" + $StrippedProductId$+"."
	logWarning "Use o4i_{{O_SOFTWARE}} instead! This will also remove (dfn_{{O_SOFTWARE}} from the client automatically)"
	logWarning "dfn_" + $StrippedProductId$ + " can be removed then from your depot."
	logWarning "*******************************************************************************************"
	Message "Updating status information for this package on depot"
	set $result$ = productSetActionRequest($ProductId$, "none")
	set $result$ = "Package is deprecated!"
	set $result$ = currentProductSetReport($result$)
	set $result$ = productExtUpdateOnClient($ProductId$, "installed", "installed", "None", $result$)
	isSuspended
endif
if  ("{{legacy_level}}" = "2")
	logError "*******************************************************************************************"
	logError "This package is DEPRECATED!"
	logError "This package will not be maintained any longer!"
	logError "The functionality is completely taken over by o4i_" + $StrippedProductId$+"."
	logError "Use o4i_{{O_SOFTWARE}} instead! (dfn_{{O_SOFTWARE}} will be removed then automatically)"
	logError "dfn_" + $StrippedProductId$ + " should be removed the then from your depot."
	Message "Updating status information for this package on depot"
	set $result$ = productSetActionRequest($ProductId$, "none")
	set $result$ = "Package is deprecated!"
	set $result$ = currentProductSetReport($result$)
	set $result$ = productExtUpdateOnClient($ProductId$, "installed", "installed", "None", $result$)
	isSuspended
endif
if  ("{{legacy_level}}" INT>= "3")
	logError "*******************************************************************************************"
	logError "This package is END OF LIFE!"
	logError "This package will not be maintained any longer!"
	logError "The functionality is completely taken over by o4i_" + $StrippedProductId$+"."
	logError "Use o4i_{{O_SOFTWARE}} instead!"
	logError "dfn_" + $StrippedProductId$ + " should be removed the then from your depot."
	logError "*******************************************************************************************"
	logError "This is a FATAL ERROR!"
	logError "*******************************************************************************************"
	Message "Updating status information for this package on depot"
	set $result$ = productSetActionRequest($ProductId$, "none")
	set $result$ = "Package is end of life!"
	set $result$ = productExtUpdateOnClient($ProductId$, "forbidden", "not_installed", "failed", "Discontinued package!")
	isSuspended
endif
{{/ifdef_legacy}}
 

;======================================================================
[Files_CopyLocalInstaller]
;======================================================================
copy "$InstallerSrc$" "$TempDir$"

;======================================================================
[Files_DeleteLocalInstaller]
;======================================================================
delete -f "$TempDir$\$InstallerFile$"

;======================================================================
[Files_DeletePackageCache]
;======================================================================
delete -sf "$PackageCacheDir$"

;======================================================================
[Winbatch_install]
;======================================================================
"$Installer$" $Inst_Opts$ 
 
;=====================================================================
[LinkFolder_CreateDesktopLink_AnacondaPrompt]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $ProductName$ Prompt
       target: "%windir%\System32\cmd.exe"
       parameters: /K ""$InstDir$\Scripts\activate.bat" "$InstDir$""
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\Iconleak-Atrous-Console.ico
       icon_index:
end_link

;=====================================================================
[LinkFolder_CreateDesktopLink_AnacondaPowerShell]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $ProductName$ PowerShell
       target: "%windir%\System32\WindowsPowerShell\v1.0\powershell.exe"
       parameters: -ExecutionPolicy ByPass -NoExit -Command "& '$InstDir$\shell\condabin\conda-hook.ps1' ; conda activate '$InstDir$' "
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\Iconleak-Atrous-Console.ico
       icon_index:
end_link

;=====================================================================
[LinkFolder_CreateDesktopLink_QtConsole]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: Jupyter QtConsole
       target: $InstDir$\Scripts\jupyter-qtconsole.exe
       parameters:
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\Iconleak-Atrous-Console.ico
       icon_index:
end_link

;=====================================================================
[LinkFolder_CreateDesktopLink_Jupyter]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: Jupyter Notebook
       target: $AppTarget$
       parameters: -q "$InstDir$\cwp.py" "$InstDir$" "$InstDir$\python.exe" "$InstDir$\Scripts\jupyter-notebook-script.py" %USERPROFILE%
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\jupyter.ico
       icon_index:
end_link

;=====================================================================
[LinkFolder_CreateDesktopLink_Spyder]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: Spyder
       target: $AppTarget$
       parameters: -q "$InstDir$\cwp.py" "$InstDir$" "$InstDir$\pythonw.exe" "$InstDir$\Scripts\spyder-script.py"
       working_dir: %HOMEPATH%
       icon_file: $InstDir$\Menu\spyder.ico
       icon_index:
end_link

;=====================================================================
[LinkFolder_CreateDesktopLink_Navigator]
;=====================================================================
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
       name: $AppName$
       target: $AppTarget$
       parameters: $AppParameters$
       working_dir: %CommonProfileDir%\Documents
       icon_file: $AppIcon$
       icon_index:
end_link

;=====================================================================
[sub_end]
;=====================================================================

