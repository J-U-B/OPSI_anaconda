;======================================================================
; update.opsiscript ANACONDA
; 
; J. Boettge <boettge@mpi-halle.mpg.de>  2018-06-26 12:24:16 +0200
;======================================================================

;======================================================================
[Initial]
;======================================================================
setLogLevel=5
; Log Errors in Logfile and abort:
ExitOnError=true
; Show syntax errors in the script:
ScriptErrorMessages=off
; Dont trace step by step through the script:
TraceMode=off 
; Let started programs run in front of the winst window
StayOnTop=false 

;======================================================================
[Actions]
;======================================================================
include_insert "%ScriptPath%\product_variables.opsiinc"

if ($pr_SkipUpdate$ = "true")
	Message "Update skipped by property"
else
	if not (FileExists($ActivateBat$))
		logError "Can't find " + $ActivateBat$
		isFatalError "activate.bat not found"
	endif	

	;=== let's go
	marktime

	ShowBitmap $Img_Update$ $ProductName$

	set $CondaOpts$ = ""
	if ($pr_VerboseUpdate$ = "true")
		set $CondaOpts$ = $CondaOpts$ + " -v"
	endif
	if ($pr_DryUpdate$ = "true")
		set $CondaOpts$ = $CondaOpts$ + " --dry-run"
	endif

	;=== run update
	Message "Updating " + $ProductName$ + " " + $Version$+ " (" + $sw_arch$ + " bit)"
	setLogLevel = 7
	set $ResultList$ = getOutStreamFromSection('ShellInAnIcon_conda_update')
	setlogLevel = $LogLevelDefault$
	set $ExitCode$ = getLastExitCode
	comment "Update returned exit code " + $ExitCode$
	set $TimeDiff$ = getDiffTimeSec
	comment  "Elapsed update time: " + $TimeDiff$ + " seconds"

	;=== run cleanup
	Message "Running conda cleanup for " + $ProductName$ + " " + $Version$+ " (" + $sw_arch$ + " bit)"
	setLogLevel = 7
	set $ResultList$ = getOutStreamFromSection('ShellInAnIcon_conda_clean')
	setlogLevel = $LogLevelDefault$
	set $ExitCode$ = getLastExitCode
	comment "Conda clean returned exit code " + $ExitCode$
	set $TimeDiff$ = getDiffTimeSec
	comment  "Elapsed time after cleanup: " + $TimeDiff$ + " seconds"

	;=== fix for update (rights inheritance):
	Message "Updating access rights for " + $ProductName$
	setLogLevel = 7
	set $ResultList$ = getOutStreamFromSection('ShellInAnIcon_fix_rights')
	setlogLevel = $LogLevelDefault$
	set $ExitCode$ = getLastExitCode
	comment "Fix rights returned exit code " + $ExitCode$

	;=== finally measure installation time
	set $TimeDiff$ = getDiffTimeSec
	Message  "Total update time: " + $TimeDiff$ + " seconds"
endif

;=====================================================================
[ShellInAnIcon_conda_update]
;=====================================================================
"$ActivateBat$" "$InstallDir$." && $CondaUpdateCmd$ $CondaOpts$
exit %ERRORLEVEL%

;=====================================================================
[ShellInAnIcon_conda_clean]
;=====================================================================
"$ActivateBat$" "$InstallDir$." && $CondaCleanCmd$ $CondaOpts$
exit %ERRORLEVEL%

;=====================================================================
[ShellInAnIcon_fix_rights]
;=====================================================================
icacls "$InstallDir$\*" /inheritance:e /T /C /L /Q
exit %ERRORLEVEL%
